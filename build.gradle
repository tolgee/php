buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
}

group 'io.tolgee'
apply plugin: 'idea'
apply from: "./gradle/e2e.gradle"

idea {
    module {
        sourceDirs += file('core/src')
        testSourceDirs += file('core/tests')
    }
}

static def dockerBase(mountDir, workdir) {
    ["docker", "run", "-v", "$mountDir:/data", "-w", "/data/$workdir", "tolgee/php:7.3-cli"]
}

task composerInstall(type: Exec) {
    outputs.dir("${project.projectDir}/vendor")
    inputs.file("${project.projectDir}/composer.json")
    inputs.file("${project.projectDir}/composer.lock")
    commandLine dockerBase(project.projectDir.absoluteFile.toString() + "", "") + ["composer", "install"]
}

task testAppNpmInstall(type: Exec) {
    outputs.dir("${project.projectDir}/test_app/node_modules")
    inputs.file("${project.projectDir}/test_app/package-lock.json")
    workingDir = "${project.projectDir}/test_app"
    commandLine "npm", "install"
    mustRunAfter composerInstall
}

task unitTest(type: Exec) {
    dependsOn composerInstall
    commandLine dockerBase(project.projectDir.absoluteFile.toString() + "", "") + ["./vendor/bin/phpunit"]
}

task buildDockerPhp(type: Exec, group: "docker") {
    commandLine "docker", "build", "-t", "tolgee/php:7.3-cli", "docker"
}

task build(type: Task) {
    dependsOn "composerInstall", "unitTest", "runE2e"
}